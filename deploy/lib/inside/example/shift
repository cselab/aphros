#!/bin/sh

me=shift
: ${AWK=awk}

usg () {
    cat <<!
$me -o idx|shift a.dat b.dat
!
    exit 2
}

Output=
while :
do case "$1" in
       -h) usg
	   ;;
       -o) shift
	   Output=$1; shift
	   case "$Output" in
	       idx|shift) ;;
	       *) printf >&2 "%s: unknown output type '%s'\n" $me $Output
		  exit 2
		  ;;
	   esac
	   ;;
       -*) printf >&2 "%s: unknown option '%s'\n" $me
	   exit 2
	   ;;
       *) break
	  ;;
   esac
done

if test -z "$Output"
then
    printf >&2 "%s: -o is not set\n" $me
    exit 2
fi

if test $# -ne 2
then
    printf >&2 "%s: needs two file names\n" $me
    exit 2
fi

"$AWK" -v Output=$Output '
BEGIN {
    eps = 1e-4
    me = "shift"
    n = read(ARGV[1], x, y)
    m = read(ARGV[2], u, v)
    if (n != m) {
	printf "%s: wrong number of vertices (n=%d, m=%d)\n", me, n, m | "cat >&2"
	exit(2)
    }
    shift_max(x, y)
    shift_max(u, v)
    for (i in x) {
	a[i] = x[i] - u[i]
	b[i] = y[i] - v[i]
    }
    for (i = 1; i <= n; i++) {
	for (j = 1; j < i; j++) {
	    d = (a[i] - a[j])^2 + (b[i] - b[j])^2
	    if (d < eps*eps)
		break
	}
	if (j == i) {
	    set[i] = ++cnt
	    dx[set[i]] = a[i]
	    dy[set[i]] = b[i]
	} else
	    set[i] = set[j]
    }

    if (Output == "idx")
	for (i = 1; i <= n; i++)
	    print set[i] - 1
    else
	for (i = 1; i <= cnt; i++)
	    printf "%-.16e %-.16e\n", dx[i], dy[i]
}

function read(f, x, y,   i) {
    while (getline < f) {
	i++
	x[i] = $1
	y[i] = $3
    }
    close(f)
    return i
}

function shift_max(x, y,   i, j, xj, yj) {
    j = max_arg(x)
    xj = x[j]
    yj = y[j]
    for (i in x) {
	x[i] -= xj
	y[i] -= yj
    }
}

function max_arg(x,   i, m, im) {
    for (i in x) {
	m = x[i]
	break
    }
    for (i in x)
	if (x[i] >= m) {
	    m = x[i]
	    im = i
	}
    return im
}

' "$@"
