#!/bin/sh
me="$0"
${AWK=awk} -v me="$me" '
BEGIN {
    FS = " *, *"
    if (ARGC < 2)
        err("not enough arguments")

    for (i = 1; i in ARGV; i++) {
        f = ARGV[i]
        one(f)
    }
}

function one(f,   r, i, cl, vf)
{
    r = getline < f
    if (r != 1)
        err("fail to read \"%s\"", f)
    for (i = 1; i <= NF; i++)
        Key[$i] = i
    while (getline < f == 1) {
        xx = get("xx")
        yy = get("yy")
        zz = get("zz")
        x = get("x")
        y = get("y")
        z = get("z")
        vf = get("vf")
        cl = get("cl")
        rg = xx + yy + zz - (x^2 + y^2 + z^2)
        if (rg < 0)
           rg = 0
        rg = sqrt(5*rg/3)
        if (cl != 0 && vf < 0.1)
            print rg
    }
    close(f)
}

function msg(fmt, a, b, c, d, e)
{
    printf "%s: " fmt "\n", me, a, b, c, d, e | "cat >&2"
}

function err(fmt, a, b, c, d, e)
{
    msg(fmt, a, b, c, d, e)
    exit(2)
}

function get(key,   i)
{
    i = Key[key]
    return $i
}

' "$@"
