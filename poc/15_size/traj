#!/bin/sh
me="$0"
${AWK=awk} -v me="$me" '
BEGIN {
    FS = " *, *"
    OFS = ","
    if (ARGC < 2)
        err("not enough arguments")

    i = 1
    cl0 = ARGV[i++]
    if (!intp(cl0))
        err("not an integer \"%s\"", cl0)

    for (; i in ARGV; i++) {
        f = ARGV[i]
        one(f)
    }
}

function one(f,   r, i, cl, vf, t, rc)
{
    t = f2time(f)
    if (empty(t))
        err("fail to extract time from \"%s\"", f)
    if (getline < f != 1)
        err("fail to read \"%s\"", f)
    for (i = 1; i <= NF; i++)
        Key[$i] = i
    while (getline < f == 1) {
        xx = get("xx")
        yy = get("yy")
        zz = get("zz")
        x = get("x")
        y = get("y")
        z = get("z")
        r = get("r")
        vf = get("vf")
        cl = get("cl")
        rg = xx + yy + zz - (x^2 + y^2 + z^2)
        if (rg < 0)
           rg = 0
        rg = sqrt(5*rg/3)
        if (cl == cl0 && vf < 0.1) {
            if (!(cl in Seen)) {
                Seen[cl]
                print "x", "y", "z", "r", "rg", "t"
            }
            print x, y, z, r, rg, t
        }
    }
    close(f)
}

function msg(fmt, a, b, c, d, e)
{
    printf "%s: " fmt "\n", me, a, b, c, d, e | "cat >&2"
}

function err(fmt, a, b, c, d, e)
{
    msg(fmt, a, b, c, d, e)
    exit(2)
}

function get(key,   i)
{
    i = Key[key]
    return $i
}

function f2time(f,   time)
{
    time = f
    sub(/.*_/, "", time)
    sub(/\..*/, "", time)
    return time + 0
}

function empty(s)
{
    return length(s) == 0
}

function intp(s)
{
    return s ~ /^[0-9][0-9]*$/
}

' "$@"
