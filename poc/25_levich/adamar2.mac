load("cgrind");
psy(f):= (diff(f,r,3)*r^2)/2+3*diff(f,r,2)*r+2*diff(f,r,1);
phi(f):= -(diff(f,r,1)*r+2*f)/2;

u(f):= f * cos(t);
v(f):= phi(f) * sin(t);
p(mu, f):= mu*psy(f) * cos(t);

f0: b1/r^3 + b2/r + b3;
f1: a1*r^2 + a2;

u0: u(f0);
v0: v(f0);
p0: p(mu0, f0) + pi;
u1: u(f1);
v1: v(f1);
p1: p(mu1, f1);

e0: limit(u0, r, inf) = U, t = 0;
e1: u0, r = a, t = 0;
e2: u1, r = a, t = 0;
e3: v0 - v1, r = a, t = %pi/2, factor;

prr(mu, p, u) := -p + 2*mu*diff(u, r);
prt(mu, p, u, v) := mu*(1/r*diff(u, t) + diff(v, r) - v/r);

prr0: prr(mu0, p0, u0), factor;
prr1: prr(mu1, p1, u1), factor;

prt0: prt(mu0, p0, u0, v0), factor;
prt1: prt(mu1, p1, u1, v1), factor;

e4: prr0 - prr1, t = 0, r = a;
e5: prt0 - prt1, t = %pi/2, r = a;

%s: linsolve([e0, e1, e2, e3, e4, e5], [a1, a2, b1, b3, b2, U]);
U0: U, %s, factor;

limit(U0, mu1, inf), factor;

v1/U, %s, factor;

