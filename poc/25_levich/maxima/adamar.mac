depends([f, phi, psy], r) $
u: f*cos(t);
v: phi*sin(t);
p: mu*psy*cos(t);

nsr(u, v, p):=
diff(p, r)
- mu *
(
  + diff(u, r, 2)
  + 1/r^2 * diff(u, t, 2)
  + 2/r*diff(u, r)
  + cot(t)/r^2 * diff(u, t)
  - 2/r^2 * diff(v, t)
  - 2*u/r^2
  - 2*cot(t)/r^2 * v
);

nst(u, v, p):=
1/r*diff(p, t)
- mu *
(
  + diff(v, r, 2)
  + 1/r^2 * diff(v, t, 2)
  + 2/r*diff(v, r)
  + cot(t)/r^2 * diff(v, t)
  + 2/r^2 * diff(u, t)
  - v/(r^2*sin(t)^2)
);

div(u, v):=diff(u, r) + 1/r*diff(v, t) + 2*u/r + v*cot(t)/r;

/*
prr = -p + 2*mu*diff(v, r);
prt = mu*(1/r*diff(v, t) + diff(u, r) - u/r); */

%d: trigsimp(div(u, v));
%nsr: trigsimp(nsr(u, v, p));
%nst: trigsimp(nst(u, v, p));

%ph0: assoc('phi, linsolve(%d, 'phi));

%nst: %nst, phi = %ph0;
%psy: assoc('psy, linsolve(%nst, psy));
%psy: ev(%psy, diff, expand);

%nsr: %nsr, phi = %ph0, psy = %psy;
%nsr: solve(ev(%nsr, diff), f)[1];
%nsr: %nsr, f = r^n, diff, factor;
solve(%nsr, n);