#!/usr/bin/env gnuplot

set macros
reset

set terminal pdfcairo color font "Helvetica,14" size 3.2,2.2 enhanced

c0='#1f77b4'
c1='#ff7f0e'
c2='#2ca02c'
c3='#d62728'
c4='#9467bd'
c5='#8c564b'

set linetype  1 lc rgb c0 lw 2
set linetype  2 lc rgb c2 lw 2
set linetype  3 lc rgb c1 lw 2
set linetype  4 lc rgb c3 lw 2
set linetype  5 lc rgb c4 lw 2
set linetype  6 lc rgb c5 lw 2
set linetype cycle 6

set style line 1 lt 1 pt 7 ps 0.5 pi 5
set style line 2 lt 2 pt 5 ps 0.5 pi 5
set style line 3 lt 3 pt 8 ps 0.5 pi 5
set style line 4 lt 4 pt 2 ps 0.5 pi 5 dt 2
set style line 5 lt 5 pt 2 ps 0.5 pi 5

set output "rn.pdf"

set key bottom

set xlabel "t [ms]"
set ylabel "r / R_{avg}"

sim = 0.27 / 44
ravg = 0.15

# from time2.py
# capillary time [ms]
tcap = 0.00814 * 1e3
# ravg from paper, (Rt+Rb)*0.5 [mm]
rtb = (1.59 + 1.01) * 0.5   # mm
# top bubble radius x [mm]
b1rx = 0.0013177 * 1e3
# sim top bubble radius x
s_b1rx = 0.1622
# length ratio sim/exp[mm]
lsc = s_b1rx / b1rx


plot \
"nx256/ch/neck" u (column("i")*sim*tcap):((0.5-column("x0"))/lsc/rtb) w lp ls 1 t "sim256"  \
, "nx512/ch/neck" u (column("i")*sim*tcap):((0.5-column("x0"))/lsc/rtb) w lp ls 2 t "sim512"  \
, "../refthor/rn" u ($1):($2) w p pt 7 ps 0.1 lc rgb "black" t "Thoroddsen,2005"
